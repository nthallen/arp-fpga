--
-- VHDL Test Bench idx_fpga_lib.bench_cmd_proc.cmd_proc_tester
--
-- Created:
--          by - nort.UNKNOWN (NORT-NBX200T)
--          at - 08:51:11 01/22/2011
--
-- Generated by Mentor Graphics' HDL Designer(TM) 2009.2 (Build 10)
--
LIBRARY ieee;
USE ieee.std_logic_1164.all;
USE ieee.std_logic_arith.all;


ENTITY bench_cmd_proc IS
   GENERIC (
      CMD_PROC_N_CMDS : integer := 24
   );
END bench_cmd_proc;


LIBRARY idx_fpga_lib;


ARCHITECTURE rtl OF bench_cmd_proc IS

   -- Architecture declarations

   -- Internal signal declarations
   SIGNAL cmd_dio : std_ulogic_vector( CMD_PROC_N_CMDS*2-1 DOWNTO 0 );
   SIGNAL cmd_out : std_ulogic_vector( CMD_PROC_N_CMDS-1 DOWNTO 0 );
   SIGNAL CmdStrb : std_ulogic;
   SIGNAL CmdEnbl : std_ulogic;
   SIGNAL RST     : std_ulogic;


   -- Component declarations
   COMPONENT cmd_proc
      GENERIC (
         CMD_PROC_N_CMDS : integer := 24
      );
      PORT (
         cmd_dio : IN     std_ulogic_vector( CMD_PROC_N_CMDS*2-1 DOWNTO 0 );
         cmd_out : OUT    std_ulogic_vector( CMD_PROC_N_CMDS-1 DOWNTO 0 );
         CmdStrb : IN     std_ulogic;
         CmdEnbl : IN     std_ulogic;
         RST     : IN     std_ulogic
      );
   END COMPONENT;

   -- embedded configurations
   -- pragma synthesis_off
   FOR DUT_cmd_proc : cmd_proc USE ENTITY idx_fpga_lib.cmd_proc;
   -- pragma synthesis_on

BEGIN

   DUT_cmd_proc : cmd_proc
      GENERIC MAP (
         CMD_PROC_N_CMDS => CMD_PROC_N_CMDS
      )
      PORT MAP (
         cmd_dio => cmd_dio,
         cmd_out => cmd_out,
         CmdStrb => CmdStrb,
         CmdEnbl => CmdEnbl,
         RST     => RST
      );

  test_proc : Process is
    -- pragma synthesis_off
    Procedure check_cmd( i: integer; curval, setval : std_ulogic ) is
      variable initval : std_ulogic;
    Begin
      initval := cmd_out(i);
      assert initval = curval
        report "check_cmd: initially value not correct"
        severity error;
      if setval = '1' then
        cmd_dio(2*i) <= '1';
        cmd_dio(2*i+1) <= '0';
      else
        cmd_dio(2*i) <= '0';
        cmd_dio(2*i+1) <= '1';
      end if;
      wait for 50 ns;
      CmdStrb <= '1';
      wait for 200 ns;
      CmdStrb <= '0';
      wait for 50 ns;
      cmd_dio(2*i) <= '0';
      cmd_dio(2*i+1) <= '0';
      if CmdEnbl = '1' then
        assert cmd_out(i) = setval
          report "check_cmd: cmd not set"
          severity error;
      else
        assert cmd_out(i) = initval
          report "check_cmd: cmd changed w/o CmdEnbl"
          severity error;
      end if;
    End Procedure;
    -- pragma synthesis_on
  Begin
    -- pragma synthesis_off
    RST <= '1';
    cmd_dio <= (others => '0');
    CmdStrb <= '0';
    CmdEnbl <= '0';
    wait for 500ns;
    RST <= '0';
    CmdEnbl <= '1';
    for i in 0 to CMD_PROC_N_CMDS-1 loop
      check_cmd(i, '0', '1');
      check_cmd(i, '1', '0');
    end loop;
    CmdEnbl <= '0';
    for i in 0 to CMD_PROC_N_CMDS-1 loop
      check_cmd(i, '0', '1');
    end loop;
    wait;
    -- pragma synthesis_on
  End Process;

END rtl;